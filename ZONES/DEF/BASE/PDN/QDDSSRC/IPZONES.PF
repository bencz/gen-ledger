      *  FIELD USAGE:
      *  ZSTS :Zone status (1).
      *        The status codes are defined and used by the users.
      *
      *  (c) COPYRIGHT ISLAND PACIFIC, INC.
     A*  PF  08/09/99  No changes. Recompiled to create IPSTOREN.
     A*  PG  01/07/00  ADDED UNIQUE KEYWORD.
     A*                ADDED EXPANSION FIELD (Z###).
     A*  GW  04/28/04  RECOMPILIED.
OPEN  *  DW  07/10/07  Use command IpDBchgPF to change field type from
OPEN  *                A to O if the field length is greater than or
OPEN  *                equal to 4.
     A*  VSU 01/26/18  ADDED SUPPORT/FIELDS FOR MULTI BUSINESS
     A*                -ZPGC > PRICE GROUP CODE
     A*                -ZCUR > ZONE CURRENCY CODE
     A*      01/30/18  -ZACT > ZONE ACTIVE('YES'=ACTIVE;'NO'=NOT ACTIVE)
     A*      02/27/18   Zone activity ZACT is used by define stores procedure
     A*                 to verify if the zone is active or not. If active
     A*                 then define stores will not allow any stores to assign
     A*                 the zones.
     a*                 The above is true only when Multi-business(PQ140)
     A*                 is enabled.
     A*  @@
     A*¸ THIS FILE IS USED BY THE FOLLOWING JOIN FILES:
     A*¸ IPTRTRYB, IPTRTRYC
     A*¸ IPREGONB, IPREGONC
     A*¸ IPDISTSB, IPDISTSC
     A*¸ IPSTOREN, IPSTOREO
     A*
     A*  ZONE CODE MASTER FILE
     A*
     A                                      UNIQUE
     A          R ZONES                     TEXT('ZONE CODES')
     A*
     A            ZZON           3S 0       TEXT('ZONE CODE')
     A            ZSTS           1A         TEXT('ZONE STATUS')
OPEN A            ZNAM          25O         TEXT('ZONE NAME')
OPEN A            ZMGR          25O         TEXT('ZONE MANAGER NAME')
OPEN A            ZPNM          22O         TEXT('PHONE NUMBER')
OPEN A            ZAD1          25O         TEXT('ADDRESS LINE ONE')
OPEN A            ZAD2          25O         TEXT('ADDRESS LINE TWO')
OPEN A            ZCTY          20O         TEXT('CITY')
     A            ZSTA           3A         TEXT('STATE')
OPEN A            ZZIP          10O         TEXT('POSTAL CODE')
OPEN A            ZCNT          20O         TEXT('COUNTRY')
     A            ZASC           2S 0       TEXT('ACCOUNTING SYS CODE')
OPEN A            Z###          25O         TEXT('UNUSED')
OPEN A            ZPGC           3A         TEXT('PRICE GROUP CODE')
OPEN A            ZCUR           3A         TEXT('ZONE CURRENCY')
OPEN A            ZACT           1A         TEXT('ACTIVE (Y/N)')
     A*
     A          K ZZON
