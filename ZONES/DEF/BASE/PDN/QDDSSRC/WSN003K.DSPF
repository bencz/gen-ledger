      *  (c) COPYRIGHT ISLAND PACIFIC, INC.
     A*  GO  02/26/01  ADD ERRMSG WHEN USER ENTERS DUPLICATE DESCRIPTION.
     A*  MAA 06/26/03  4 TO 2 DECIMALS IN R1.5 FINANCE.
     A*  GO  02/11/04  CHANGE USE OF IND 20 TO 30 TO MAKE FOLLOW STDS.
OPEN  *  DW  07/11/07  Use command IpDBchgWSN to change field type from
OPEN  *                A to O if the field length is greater than or
OPEN  *                equal to 4.
     A*  VSU 01/30/18  MULTI BUSINESS LOGIC: ZONE LEDGER
     A*      01/31/18  CONTINUE..
     A*      02/02/18  CONTINUE..
     A*      02/05/18  fix indicator usage for ZCUR and ZPGC.
     A*      03/13/18  CHANGE CURRENCY TO SHOW/HIDE BASED ON CPQ100
     A*  VSU 04/09/18  Protect Price group and Currency for existing zones
      *  VSU 04/26/18  -Do not allow same zones to be added again, when the zone
      *                 exists in IPIZHDR.
      *                ¹"When a Zone is deleted, the IPIZHDR records remains unt
      *                ¹the user runs IPAUDZONE for that zone in Update Mode.
      *                ¹Added validation so if the user attempts to add the same
      *                ¹zone through Define zones before IPIZHDR records are cle
      *                ¹-ared from the definition, we show error "Cannot add zon
      *                ¹-e while zone header records exists" and do not allow th
      *                ¹the zone zone to be added until those records are clear"
     A*  @@
     A*
     A*  DEFINE ZONES
     A*
     A                                      CHGINPDFT HELP(01) PRINT(IPPRINT)
     A                                      ROLLUP(91) ROLLDOWN(92)
     A                                      CA03
     A*
     A*  SCREEN HEADER
     A*
     A                                      MSGLOC(24) ENTFLDATR((*DSPATR RI))
     A                                      ALTHELP(CA01)
     A          R FM01                      OVERLAY LOCK
     A                                      KEEP
OPEN A            CNAM          25O  O  1  2
     A                                  1 28'          DEFINE ZONES          '
     A            WSID           3   O  1 68
     A            CDSP           6Y 0O  1 72EDTCDE(Y)
     A*
     A*  ENTER ZONE CODE
     A*
     A          R FM02                      BLINK OVERLAY PROTECT PUTOVR
     A  19                                  CF24
     A                                  4  2'Zone code . . . . . . . . . .'
     A            ZZON           3  0B  4 32RANGE(1 999) CHECK(RZ AB)
     A                                      EDTCDE(X) OVRDTA
     A                                      DSPATR(UL MDT) BLANKS(23)
     A  03                                  ERRMSGID(IPU0231 WSN003K)
     A  04                                  ERRMSGID(E020004 WSN003K)
     A  08                             22  2'The maximum number of zones (99) h-
     A                                      ave been defined.'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit   ROLL=Next               -
     A                                                                         -
     A                                               '
     A                                      COLOR(BLU)
     A*
     A*  ENTER ZONE INFORMATION
     A*
     A          R FM03                      BLINK OVERLAY PROTECT
     A                                      CA23 CA12 PUTOVR
     A  81                                  CF04(94)
     A                                      CSRLOC(@DFCRW     @DFCCL)
     A                                      RTNCSRLOC(&@DFFMT &@DFFLD)
     A            @DFCRW         3S 0H      TEXT('POSITION CURSOR AT ROW')
     A            @DFCCL         3S 0H      TEXT('POSITION CURSOR AT COLUMN')
     A            @DFFMT        10A  H      TEXT('RECORD FORMAT WITH CURSOR')
     A            @DFFLD        10A  H      TEXT('FIELD WITH CURSOR')
     A  16        @DFMSG        78   M
     A                                  4  2'Zone code . . . . . . . . . .'
     A            ZZON           3  0O  4 32
     A                                      EDTCDE(X) OVRDTA
     A                                  6  2'Zone name . . . . . . . . . .'
OPEN A            ZNAM          25O  B  6 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A  04                                  ERRMSGID(IPU0232 WSN003K)
     A  17                                  ERRMSGID(E030017 WSN003K)
     A  05                                  ERRMSGID(IPU0233 WSN003K)
     A  07                                  ERRMSGID(E030007 WSN003K)
     A                                  9  2'Manager''s name  . . . . . . .'
OPEN A            ZMGR          25O  B  9 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 10  2'Telephone number  . . . . . .'
OPEN A            ZPNM          22O  B 10 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 11  2'Address line 1  . . . . . . .'
OPEN A            ZAD1          25O  B 11 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 12  2'Address line 2  . . . . . . .'
OPEN A            ZAD2          25O  B 12 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 13  2'City  . . . . . . . . . . . .'
OPEN A            ZCTY          20O  B 13 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 14  2'State . . . . . . . . . . . .'
     A            ZSTA           3   B 14 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 15  2'Postal code . . . . . . . . .'
OPEN A            ZZIP          10O  B 15 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 16  2'Country . . . . . . . . . . .'
OPEN A            ZCNT          20O  B 16 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A                                 17  2'Status  . . . . . . . . . . .'
     A            ZSTS           1   B 17 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A  30                             19  2'Financial company . . . . . .'
     A  30        ZASC           2  0B 19 32OVRDTA EDTCDE(X)
     A  30N02                               DSPATR(HI)
     A  30 02                               DSPATR(UL)
     A  06                                  ERRMSGID(E030006 WSN003K)
     A  30 09                               ERRMSGID(E030009 WSN003K)
     A  30 12                               ERRMSGID(E030012 WSN003K)
     A  80                             20  2'Price group code. . . . . . .'
     A  80        ZPGC           3A  B 20 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A N40                                  DSPATR(PR)
     A  13                                  ERRMSGID(E030013 *LIBL/WSN003K)
     A  14                                  ERRMSGID(E030014 *LIBL/WSN003K)
     A  81N40                          21  2'Currency code . . . . . . . .'
     A  81 40                          21  2'Currency code . . . . . . . ?'
     A  81        ZCUR           3A  B 21 32OVRDTA
     A N02                                  DSPATR(HI)
     A  02                                  DSPATR(UL)
     A N40                                  DSPATR(PR)
     A  15                                  ERRMSGID(E030015 *LIBL/WSN003K)
/CF/ A N81N40                          23  2'F3=Exit   F12=Cancel   F23=Delete -
     A                                        ROLL=Next                        -
     A                                               '
     A                                      COLOR(BLU)
/CF/ A  81 40                          23  2'F3=Exit   F4=Prompt   F12=Cancel  -
     A                                       F23=Delete   ROLL=Next'
     A                                      COLOR(BLU)
/CF/ A  81N40                          23  2'F3=Exit   F12=Cancel   F23=Delete -
     A                                        ROLL=Next                        -
     A                                               '
     A                                      COLOR(BLU)
