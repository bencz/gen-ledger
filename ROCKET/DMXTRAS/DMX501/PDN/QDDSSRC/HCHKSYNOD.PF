      *CP: SIZE(*NOMAX)
      * ============================================================ *
      * Object:    HCHKSYNOD       *FILE PF                          *
      *            Holds the retrieved object descriptions.          *
      *                                                              *
      * Create remarks: CRTPF SIZE(*NOMAX)                           *
      *                                                              *
      * ------------------------------------------------------------ *
      *             Changes made to this source                      *
      *             ---------------------------                      *
      *                                                              *
      * Date     Who Description                                     *
      * -------- --- ----------------------------------------------- *
      * 20001011 FK  Initial delivery                                *
      * 20001205 FK  Handle different target library                 *
      * 20021111 FK  Include retrieval date                          *
      * 20021119 FK  Include target/group                            *
      * 20050204 FK  Include journal information                     *
      * 20051010 FK  Include target library                          *
      * 20051230 FK  Exclude flag instead of delete of record        *
      * 20060301 FK  Show if object locked/damaged                   *
      * 20060323 FK  Include indicator referenced in HA(X)MOBJ       *
      * 20060327 FK  Include original object name                    *
      * 20080107 FK  Object specifier sequence                       *
      * 20111217 FK  REMOVE HA                                       *
      * ============================================================ *
     A          R SYNOD@
     A            ODGRPN        10A         TEXT('Group name')
     A                                      COLHDG('Group name')
     A            ODCMPL        10A         TEXT('Original library')
     A                                      COLHDG('Original' 'library')
     A            ODCMPO        10A         TEXT('Original object')
     A                                      COLHDG('Original' 'object')
     A            ODLBNM        10A         TEXT('Object library')
     A                                      COLHDG('Object' 'library')
     A            ODOBNM        10A         TEXT('Object name')
     A                                      COLHDG('Object' 'name')
     A            ODOBTP         8A         TEXT('Object type')
     A                                      COLHDG('Object' 'type')
     A            ODOBAT        10A         TEXT('Object attribute')
     A                                      COLHDG('Object' 'attribute')
     A            ODTGTL        10A         TEXT('Target library')
     A                                      COLHDG('Target' 'library')
     A            ODSEQ          3P 0       TEXT('Object spec. sequence')
     A                                      COLHDG('Obj. spec.' 'Sequence#')
     A            ODCCEN         1A         TEXT('Creation century')
     A                                      COLHDG('Create' 'Century')
     A            ODCDAT         6A         TEXT('Creation date')
     A                                      COLHDG('Create' 'Date')
     A            ODCTIM         6A         TEXT('Creation time')
     A                                      COLHDG('Create' 'Time')
     A            ODLCEN         1A         TEXT('Change century')
     A                                      COLHDG('Change' 'Century')
     A            ODLDAT         6A         TEXT('Change date')
     A                                      COLHDG('Change' 'Date')
     A            ODLTIM         6A         TEXT('Change time')
     A                                      COLHDG('Change' 'Time')
     A            ODOBTX        50A         TEXT('Description')
     A                                      COLHDG('Description')
     A            ODOBOW        10A         TEXT('Object owner')
     A                                      COLHDG('Owner')
     A            ODOBSZ        10  0       TEXT('Object size')
     A                                      COLHDG('Size')
     A            ODSIZU        10  0       TEXT('Size in units')
     A                                      COLHDG('Size' 'Units')
     A            ODBPUN        10  0       TEXT('Bytes per unit')
     A                                      COLHDG('Bytes' 'per Unit')
     A            ODJRST         1A         TEXT('Currently journaled -
     A                                      0=No, 1=Yes')
     A                                      COLHDG('Currently' 'journaled')
     A            ODJRNM        10A         TEXT('Journal name')
     A                                      COLHDG('Journal' 'name')
     A            ODJRLB        10A         TEXT('Journal library')
     A                                      COLHDG('Journal' 'library')
     A            ODJRIM         1A         TEXT('Journal images -
     A                                      0=After, 1=Both')
     A                                      COLHDG('Journal' 'images')
     A            ODJRCN         1A         TEXT('Journal century')
     A                                      COLHDG('Journal' 'Century')
     A            ODJRDT         6A         TEXT('Journal date')
     A                                      COLHDG('Journal' 'Date')
     A            ODJRTI         6A         TEXT('Journal time')
     A                                      COLHDG('Journal' 'Time')
     A            ODOBLK         1A         TEXT('Object locked?')
     A                                      COLHDG('Object' 'Locked?')
     A            ODOBDM         1A         TEXT('Object damaged?')
     A                                      COLHDG('Object' 'Damaged?')
     A            ODDCEN         1A         TEXT('Retrieve century')
     A                                      COLHDG('Retrieve' 'Century')
     A            ODDDAT         6A         TEXT('Retrieve date')
     A                                      COLHDG('Retrieve' 'Date')
     A            ODDTIM         6A         TEXT('Retrieve time')
     A                                      COLHDG('Retrieve' 'Time')
     A            ODINC          4A         TEXT('Included/Excluded')
     A                                      COLHDG('Object' 'Included')
     A                                      VALUES('*INC' '*EXC')
     A                                      DFT('*INC')
     A            ODDOM          4A         TEXT('Data Object Mirrored')
     A                                      COLHDG('Data Object' 'Mirrored')
     A                                      VALUES('*YES' '*NO' ' ')
      *
     A          K ODGRPN
     A          K ODCMPL
     A          K ODCMPO
     A          K ODOBTP
     A          K ODSEQ                     DESCEND
