     A*%%TS  SD  20111220  095849  FKETELAARS  REL-V5R4M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A          R PNLRCD
     A                                      TEXT('Screen footer (fields)')
     A                                 23  2'F3=Exit   F5=Refresh-
     A                                         F12=Cancel   F21=Command line'
     A                                      COLOR(BLU)
     A          R SFLRCD                    SFL
     A*%%TS  SD  20070513  071721  FKETELAARS  REL-V5R2M0  5722-WDS
     A                                      TEXT('Subfile record (fields)')
     A  84                                  SFLNXTCHG
     A            DBFRRNSR       9N 0H      TEXT('DBF Relative record no')
     A            OPTISR         2A  B 10  2
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                      CHECK(AB)
     A                                      VALUES('4' '8' '12')
     A            SRJRN     R        O 10  6REFFLD(LSTJRNR/LJJRN *LIBL/HLSTJRN)
     A            SRJRNL    R        O 10 17REFFLD(LSTJRNR/LJJRNL *LIBL/HLSTJRN)
     A            SRJRNTYP  R        O 10 55REFFLD(LSTJRNR/LJJRNTYP *LIBL/HLSTJ-
     A                                      RN)
     A            SRNBRRCV  R        O 10 28REFFLD(LSTJRNR/LJNBRRCV *LIBL/HLSTJ-
     A                                      RN)
     A                                      EDTCDE(3)
     A            SRTOTSIZGBR        O 10 39REFFLD(LSTJRNR/LJTOTSIZGB *LIBL/HLS-
     A                                      TJRN)
     A                                      EDTCDE(1)
     A          R SFLCTL                    SFLCTL(SFLRCD)
     A*%%TS  SD  20111220  095849  FKETELAARS  REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      TEXT('Subfile control (fields)')
     A                                      PAGEDOWN(28)
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reload')
     A                                      CA12(12 'Cancel')
     A                                      CA21(21 'Command line')
     A                                      HOME(25)
     A  29                                  CSRLOC(#ROW       #COL)
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  95                                  SFLEND(*MORE)
     A            #RRN           4S 0H      SFLRCDNBR(CURSOR)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A                                  1  2'HWRKJRN'
     A                                      COLOR(BLU)
     A                                  1 18'Work with journals'
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  3  2'Journal(s) :'
     A                                      DSPATR(HI)
     A            SCJRN         21A  O  3 17
     A                                  3 43'Products  . . . . . :'
     A                                      DSPATR(HI)
     A            SCPRD         12A  O  3 67
     A                                  6  2'Type options and press Enter'
     A                                      COLOR(BLU)
     A                                  7  2'4=Delete journal receivers  8=Chec-
     A                                      k use  12=Work with journal attribu-
     A                                      tes'
     A                                      COLOR(BLU)
     A                                  9  2'Opt'
     A                                      DSPATR(HI)
     A                                  9  6'Journal'
     A                                      DSPATR(HI)
     A                                  9 17'Library'
     A                                      DSPATR(HI)
     A                                  9 30'# of Rcv'
     A                                      DSPATR(HI)
     A                                  9 41'Total size GB'
     A                                      DSPATR(HI)
     A                                  9 55'Type'
     A                                      DSPATR(HI)
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N30                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ
